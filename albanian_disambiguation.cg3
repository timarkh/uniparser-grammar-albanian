# DELIMITERS
DELIMITERS = "<SENT_BOUNDARY>";

# SETS
LIST Questions = "ku" "nga" "çfarë" "ç" "kur" "si" "qysh" "sa" "pse" "kush" "cili";
LIST BOS = (>>>);
LIST Verbs = vi vt med;
LIST Poss = "im" "yt" "tij" "saj" "ynë" "juaj" "tyre";
# The pronominal clitic "e" is not included in the list of clitics, because it is problematic.
LIST Clitics = "<më>"i "<ma>"i "<të>"i "<ta>"i "<i>"i "<na>"i "<ju>"i "<u>"i "<ia>"i "<ja>"i "<ua>"i "<iu>"i;
SET BOSPunct = (punct) | BOS;

SECTION    # Verbs
# 20.09.2023 Choose correct person for verbs in 1-3, sg, present tense: look at pronominal subjects
# Последнее не проверено.
SELECT (V ind pres act 1 sg) IF (0 Verbs)
								(-1 (PRO 1 sg nom))
								(NEGATE -2 ("<nga>"i) OR ("<tek>"i) OR ("<te>"i));
SELECT (V ind pres act 2 sg) IF (0 Verbs)
								(NEGATE 0 ("<do>"i))
								(-1 (PRO 2 sg nom))
								(NEGATE -2 ("<nga>"i) OR ("<tek>"i) OR ("<te>"i));
SELECT (V ind pres act 3 sg) IF (0 Verbs)
								(-1 (PRO 3 sg m nom) OR (PRO 3 sg f nom))
								(NEGATE -2 ("<nga>"i) OR ("<tek>"i) OR ("<te>"i));
								
# Imp,2,pl vs. prs,2,pl: future tense and modals 
# 20.09.2023 не получилось проверить
REMOVE (V imp) IF (-1 Questions);
REMOVE (V imp) IF (-1 Clitics OR ("<e>"i))
				  (-2 Questions);
REMOVE (V imp) IF (1 (PRO 1 sg nom) OR (PRO 2 sg nom) OR (PRO 3 sg m nom) OR (PRO 3 sg f nom));				  
REMOVE (V imp) IF (-2 Verbs)
				  (-1 Clitics or ("<të>"));		   
					  
# 20.09.2023 Analytical perfect (select "participle", not "adjective").
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (NEGATE 0 ("<sëmurë>") OR ("<përballë>"))
				 (-1 ("kam"i) OR ("jam"i));

# Aorist (select "vt, vi", not "med" in Aorist when there is no reflexive particle "u"; remove "act" after definitely reflexive "u").
REMOVE (med) IF (0 (V aor))
				(NEGATE -1 ("<u>"i) OR ("<iu>"i) OR ("<ju>"i));
REMOVE (ind pres act) IF (0 (V))
						 (-1 ("<iu>"i));
				 
# Analytical construction "për të + participle" (select "participle", not "adjective" or "noun")
# 20.09.2023 Проверены только первые два.
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<ta>"i))
				 (-2 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 Clitics OR ("<mos>"i))
				 (-2 ("<të>"i) OR ("<t>"i))
				 (-3 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 Clitics)
				 (-2 ("<mos>"i))
				 (-3 ("<të>"i) OR ("<t>"i))
				 (-4 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<të>"i) OR ("<t>"i))
				 (-4 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<ta>"i))
				 (-2 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 Clitics OR ("<mos>"i))
				 (-2 ("<të>"i) OR ("<t>"i))
				 (-3 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 Clitics)
				 (-2 ("<mos>"i))
				 (-3 ("<të>"i) OR ("<t>"i))
				 (-4 ("<për>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<të>"i) OR ("<t>"i))
				 (-4 ("<për>"i));

# Analytical constructions "duke + participle" and "pa + participle" (select "participle", not "adjective" or "noun")
# 20.09.2023 Проверены только первые два.
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 Clitics)
				 (-2 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 Clitics)
				 (-2 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<duke>"i) OR ("<tue>"i) OR ("<pa>"i));

# 20.09.2023 Analytical constructions "duke mos + participle" (select "participle", not "adjective" or "noun")
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<mos>"i))
				 (-2 ("<duke>"i) OR ("<tue>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 Clitics)
				 (-2 ("<mos>"i))
				 (-3 ("<duke>"i) OR ("<tue>"i));
SELECT (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<mos>"i))
				 (-4 ("<duke>"i) OR ("<tue>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<mos>"i))
				 (-2 ("<duke>"i) OR ("<tue>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 Clitics)
				 (-2 ("<mos>"i))
				 (-3 ("<duke>"i) OR ("<tue>"i));
SELECT (ptcp) IF (0 (ptcp) - (NOUN))
				 (-1 ("<i>"i) OR ("<e>"i))
				 (-2 ("<na>"i))
				 (-3 ("<mos>"i))
				 (-4 ("<duke>"i) OR ("<tue>"i));
				 
# "participle" vs. "adjective" after article së (не проверено)
REMOVE (ptcp) IF (0 (ptcp) - (A))
				 (-1 ("<së>"i));				 

# "noun" vs. "adjective" and "verb" after article së in "së bëri" - 
# 20.09.2023 Два последних не проверены. Добавила глагол (иначе për së dyti неправильно разбирается).
REMOVE (A abl indef) IF (0 (A) - (NOUN))
				 (-1 ("<së>"))
				 (-2 (V));
REMOVE (A gen_dat indef) IF (0 (A) - (NOUN))
				 (-1 ("<së>"))
				 (-2 (V));
REMOVE (A nom def) IF (0 (A) - (NOUN))
				 (-1 ("<së>"))
				 (-2 (V));
REMOVE (V aor 3sg) IF (0 (V) - (NOUN))
				 (-1 ("<së>"))
				 (-2 (V));
REMOVE (NOUN gen_dat indef) IF (0 (NOUN))
				 (-1 ("<së>"))
				 (-2 (V));
				 
# Optative vs. Gheg participle (dashtë, etc.).
# 20.09.2023 не все удалось проверить.
REMOVE (V vt opt pres act 3 sg) IF (-1 ("kam") OR ("jam") OR ("<me>"i) OR ("<tue>"i));
REMOVE (V vi opt pres act 3 sg) IF (-1 ("kam") OR ("jam") OR ("<me>"i) OR ("<tue>"i));
REMOVE (V vt opt pres act 3 sg) IF (-2 ("<me>"i));
REMOVE (V vi opt pres act 3 sg) IF (-2 ("<me>"i));
REMOVE (V vt opt pres act 3 sg) IF (-1 ("<u>"i))
								   (-3 ("<me>"i));
REMOVE (V vi opt pres act 3 sg) IF (-1 ("<u>"i))
								   (-3 ("<me>"i));

# Optative vs. adjective (ultë, shkathtë, flaktë, etc.).
# 20.09.2023 не все удалось проверить, вообще странное правило.
REMOVE (V vt opt pres act 3 sg) IF (-1 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
								   (-2 ("jam") OR ("<mund>"i) OR ("<do>"i) OR ("<më>"i) OR ("<tepër>"i) OR ("<shumë>"i) 
								   OR ("<mjaft>"i) OR ("<tejet>"i) OR ("<fare>"i) OR ("<sa>"i) OR ("<çdo>"i));
REMOVE (V vi opt pres act 3 sg) IF (-1 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
								   (-2 ("jam") OR ("<mund>") OR ("<do>") OR ("<më>") OR ("<tepër>") OR ("<shumë>") 
								   OR ("<mjaft>"i) OR ("<tejet>"i) OR ("<fare>"i) OR ("<sa>"i) OR ("<çdo>"i));
REMOVE (V vt opt pres act 3 sg) IF (-1 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
								   (-3 ("jam"));
REMOVE (V vi opt pres act 3 sg) IF (-1 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
								   (-3 ("jam"));
								
# Alb patë, vratë, leshtë cannot be the optative of kam, vras, leshem.
REMOVE (V vt opt pres act 3 sg) IF (0 ("<patë>"i) OR ("<vratë>"i));
REMOVE (V opt pres med act 3 sg) IF (0 ("<leshtë>"i))
									(NEGATE -1 ("<u>"i));

# 20.09.2023 Medial verbs do not have analytical perfect with "kam" (select "vt vi" participle).
REMOVE (med) IF (0 (ptcp))
				(-1 ("kam"));
				 
# Alb Gheg infinitive vs. verb-sbjv.prs.3sg
# 20.09.2023 не проверено, но должно работать
REMOVE (V sbjv) IF (-1 ("<të>"i) OR ("<t>"i) OR ("<ta>"i))
				   (-2 ("me"i));
				   
# Verbs cannot be used after "së" (not "-së" in abbreviations)
# 20.09.2023 можно объединить с "participle" vs. "adjective" after së; пока есть проблема с дефисом, не применять.
# REMOVE (V) IF (-1 ("<së>"i))
#			  (NOT -1 ("<-së>"i));

SECTION    # Nouns
# 20.09.2023 Nominative after the prepositions "tek" and "nga". 
SELECT (nom def) IF (0 (NOUN) OR (PRO) OR (ART))
					(NEGATE 0 (NUM))
					(-1 ("<tek>"i) OR ("<te>"i) OR ("<nga>"i));
					
# 20.09.2023 Accusative after the prepositions "me", "nëpër", "në" et al.
SELECT (acc) IF (0 (NOUN) OR (PRO) OR (ART))
				(NEGATE 0 (NUM))
				(-1 ("<me>"i) OR ("<në>"i) OR ("<nëpër>"i) OR ("<nën>"i) OR ("<mbi>"i) OR ("<ndër>"i));
					
# Ablative and accusative after the preposition "për"
REMOVE (nom) IF (0 (NOUN) OR (PRO) OR (ART))
				(-1 ("<për>"i));
REMOVE (gen_dat) IF (0 (NOUN) OR (PRO) OR (ART))
				(-1 ("<për>"i));
				
# Ablative after prepositions
SELECT (abl) IF (0 (NOUN) OR (PRO) OR (ART))
				(-1 ("<sipas>"i) OR ("<prej>"i) OR ("<përtej>"i) OR ("<tej>"i) OR ("<ndërmjet>"i));

# Genitive-dative in genitival phrases
SELECT (NOUN gen_dat) IF (0 (NOUN))
						 (-1 (ART))
						 (-2 (NOUN));
SELECT (NOUN gen_dat) IF (0 (NOUN))
						 (-1 ("<një>"i) OR ("ky"i) OR ("ai"i))
						 (-2 (ART))
						 (-3 (NOUN));

# 20.09.2023 Indefinite ablative in "genitival" phrases.
REMOVE (NOUN sg gen_dat indef) IF (0 (NOUN sg indef))
								  (-1 (NOUN indef));
REMOVE (NOUN sg nom indef) IF (0 (NOUN sg indef))
							  (-1 (NOUN indef))
							  (-2 ("<një>"i));

# Words immediately after një are nouns, not verbs.
REMOVE (V) IF  (0 (V) - (NOUN))
			   (-1 ("<një>"i));
				
# Nouns immediately after një are singular, not plural.
REMOVE (pl) IF  (0 (NOUN))
				(-1 ("<një>"i));
				
# 20.09.2023 Nouns immediately after numerals are plural, not singular.
REMOVE (sg) IF  (0 (NOUN))
				(NEGATE 0 ("<dhe>"i))
				(-1 (NUM))
				(NEGATE -1 ("<një>"i));
				 
# 20.09.2023 Nominative after the prepositions "tek" and "nga": indefinite contexts.
# Последнее не проверено
SELECT (nom sg indef) IF (0 (NOUN))
					     (-1 ("<një>"i))
					     (-2 ("<tek>"i) OR ("<te>"i));
SELECT (nom sg indef) IF (0 ("<tjetër>"i))
					     (-1 ("<një>"i))
					     (-2 ("<tek>"i) OR ("<te>"i) OR ("<nga>"i));
SELECT (ART nom dist) IF (0 ("<i>"i) OR ("<e>"i))
						 (1 (NOUN))
						 (NEGATE 1 ("<panë>"i) OR ("<ka>"i))
						 (-1 ("<tek>"i) OR ("<te>"i) OR ("<nga>"i));
SELECT (ART) IF (0 ("<i>"i) OR ("<e>"i))
				(1 (NOUN))
				(-1 ("<një>"i))
			    (-2 ("<tek>"i) OR ("<te>"i) OR ("<nga>"i));
				
# 20.09.2023 Unmarked case: pseudopartitive and quantitative constructions (dy kilogramë sheqer, një gotë ujë).
REMOVE (unmkd) IF (0 (NOUN))
				  (-1 (NUM) or (PRO) or (PR));
SELECT (unmkd) IF (0 (NOUN))
				  (-1 (NOUN indef))
				  (-2 (NUM));					

# 20.09.2023 Disambiguation of articulated nouns after "në".
SELECT (NOUN) IF (-1 ("<të>"i))
				 (-2 ("<në>"i))
				 (NEGATE 1 (NOUN unmkd));
				 
SECTION    # Adjectives
# Most adjectives can also work as adverbs. If an adjective has an article, it is not an adverb.
REMOVE (ADV) IF (0 (A))
                (-1 (ART));

# Substantivized adjectives can be detected when used as genitival attributes and after some prepositions.
SELECT (NOUN gen_dat) IF (0 (A gen_dat) - (NOUN gen_dat))
						 (-1 (ART))
						 (-2 (ART));
SELECT (NOUN abl) IF (0 (A) - (NOUN))
                     (-1 (ART))
				     (-2 ("sipas"i) OR ("prej"i));

# 20.09.2023 Countable words used after quantifiers are nouns, not unarticulated adjectives.
REMOVE (A) IF (0 (A) - (NOUN))
			  (-1 (NUM) OR ("<disa>"i) OR ("<ca>"i));

# 20.09.2023 Articulated adjectives do not have case and definiteness in predicates (jam e mirë).
REMOVE (A indef) IF (0 (A))
				    (-1 (ART))
				    (-2 ("jam"));
REMOVE (A indef) IF (0 (A))
					(-1 (ART))
					(-2 ("<më>"i) OR ("<ma>"i) OR ("<shumë>"i) OR ("<tepër>"i))
				    (-3 ("jam"));

# 20.09.2023 Disambiguation of prepositional adjectives i vetëm, i njëjtë.
# Noun is "unmkd" if the adjective is in preposition.
SELECT (A m sg nom def) IF (0 ("<vetmi>"i) OR ("<njëjti>"i) OR ("<ashtuquajturi>"i))
						   (-1 ("<i>"i));
SELECT (NOUN unmkd) IF (0 (NOUN))
					   (-1 ("vetëm"i) OR ("njëjtë"i) OR ("ashtuquajtur"i))
					   (-2 (ART));
					   
SECTION    # Articles
# 20.09.2023 Contact vs. distant position of the article.
REMOVE (dist) IF (0 (ART))
				 (-1 (NOUN nom def) OR (NOUN acc def));
REMOVE (cont) IF (0 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
				     (-1 Poss)
				     (1 (A) OR (NOUN gen_dat sg def) OR (NOUN gen_dat pl));
SELECT (cont gen_dat) IF (0 ("<së>"i))
						 (-1 (NOUN f sg gen_dat def));
SELECT (dist nom) IF (0 ("<i>"i) OR ("<e>"i) OR ("<të>"i))
				     (-1 ("jam"));
					 
# 20.09.2023 Contact vs. distant position of the article: i gjithë, i cili, i tërë.
SELECT (dist) IF (0 (ART))
				 (1 ("cili"i) OR ("gjithë"i) OR ("tërë"i));
REMOVE (nom) IF (0 ("<të>"i))
				(1 ("<cilëve>"i) OR ("<gjithëve>"i) OR ("<tërëve>"i) OR ("<cilave>"i) OR ("<gjithave>"i) OR ("<tërave>"i) OR ("<cilit>"i));
REMOVE (acc) IF (0 ("<të>"i))
				(1 ("<cilëve>"i) OR ("<gjithëve>"i) OR ("<tërëve>"i) OR ("<cilave>"i) OR ("<gjithave>"i) OR ("<tërave>"i) OR ("<cilit>"i));
REMOVE (gen_dat) IF (0 ("<të>"i))
					(1 ("<cilët>"i) OR ("<cilat>"i) OR ("<gjithët>"i) OR ("<gjithat>"i) OR ("<tërët>"i) OR ("<tërat>"i));
REMOVE (abl) IF (0 ("<të>"i))
				(1 ("<cilët>"i) OR ("<cilat>"i) OR ("<gjithët>"i) OR ("<gjithat>"i) OR ("<tërët>"i) OR ("<tërat>"i));
SELECT (nom sg) IF (0 ("<e>"i))
				   (1 ("<cila>"i) OR ("<gjitha>"i) OR ("<tëra>"i));
SELECT (acc) IF (0 ("<të>"i))
				(1 ("<cilën>"i) OR ("<cilin>"i) OR ("<gjithën>"i) OR ("<gjithin>"i) OR ("<tërën>"i) OR ("<tërin>"i));
SELECT (gen_dat) IF (0 ("<të>"i))
					(1 ("<cilëve>"i) OR ("<gjithëve>"i) OR ("<tërëve>"i) OR ("<cilave>"i) OR ("<gjithave>"i) OR ("<tërave>"i) OR ("<cilit>"i))
					(-1 (ART));
				 
# 20.09.2023 Contact vs. distant position of the article: NPs with Accusative prepositions "me", "në" et al.
SELECT (dist acc) IF (0 ("<të>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN acc indef))
					 (-2 (PR with_acc));
SELECT (cont acc) IF (0 ("<e>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN acc def))
					 (-2 (PR with_acc));

# 20.09.2023 Contact vs. distant position of the article: NPs with Nominative prepositions "nga", "tek" et al.
SELECT (cont nom) IF (0 ("<i>"i) OR ("<e>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN nom def))
					 (-2 (PR with_nom));
SELECT (nom) IF (0 (NOUN sg indef))
				(-1 ("<tjetër>"i))
				(-2 ("<një>"i))
				(-3 (PR with_nom));

# 20.09.2023 Contact vs. distant position of the article: NPs with Ablative prepositions "prej", "sipas" et al.
SELECT (abl pl) IF (0 ("<të>"i))
				   (1 (A) OR Poss)
				   (-1 (NOUN abl pl))
				   (-2 (PR with_abl))
				   (NEGATE -2 ("<për>"i));
SELECT (cont abl) IF (0 ("<së>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN f sg abl def))
				     (-2 (PR with_abl))
					 (NEGATE -2 ("<për>"i));
SELECT (cont abl) IF (0 ("<të>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN m sg abl def))
				     (-2 (PR with_abl))
					 (NEGATE -2 ("<për>"i));
SELECT (dist abl) IF (0 ("<të>"i))
				     (1 (A) OR Poss)
					 (-1 (NOUN sg abl indef))
				     (-2 (PR with_abl))
					 (NEGATE -2 ("<për>"i));

SECTION    # Pronouns and pronominal clitics
# Pronoun "ta" is not a clitic after prepositions (full form is used) and is a clitic before a verb.
REMOVE (PRO) IF (0 ("<ta>"i))
                (1 (V))
				(-1 (V));
				
# Pronouns "tij", "saj", "tyre" are are possesive pronouns after articles.
REMOVE ("PRO 3 pl") IF (0 ("<tyre>"i))
                   (-1 (ART));
REMOVE ("PRO 3 sg m") IF (0 ("<tij>"i))
                  (-1 (ART));
REMOVE ("PRO 3 sg f") IF (0 ("<saj>"i))
                  (-1 (ART));

# 20.09.2023 Alb i gjithë, i tërë: noun is definite after these pronouns
REMOVE (NOUN indef) IF (-1 ("gjithë"i) OR ("tërë"i))
					   (-2 (ART));

# 20.09.2023 Unarticulated gjithë do not have case and definiteness
REMOVE (indef) IF (0 ("<gjithë>"i))
				  (NEGATE -1 (ART));
				  
SECTION    # Unchangeable POS
# Reflexive particle u.
REMOVE (VB_PART) IF (0 ("<u>"i))
					(1 (V ind prs) OR (V ind ipf) OR (V sbjv))
					(NEGATE 1 (V ind prs 3sg));

# Alb të të: verb particle + pronominal clitic.
SELECT (VB_PART) IF (0 ("të"i))
					(1 ("të"i) OR ("<ta>"i))
					(2 (V))
					(NEGATE 2 (ptcp));
SELECT (CLIT_PRO) IF (0 ("<të>"i) OR ("<ta>"i))
					 (-1 ("të"i))
					 (1 (V))
					 (NEGATE 1 (ptcp));	

# Alb në immediately before the (pro)noun is a preposition, not a conjunction.
REMOVE (CONJ) IF (0 ("<në>"i))
				 (1 (NOUN acc) OR (PRO));
				 
# Constructions with më: pronominal clitic vs. article vs. "more". No verbs or indefinite adjectives after comp/sup "më" and article.
# Accusative clitic is used before transitive verbs, dative clitic before intransitive and medial verbs.
SELECT (CLIT_PRO) IF (0 ("<e>"i))
					 (1 (V vt))
					 (NEGATE 1 (ptcp))
					 (NEGATE -1 ("<më>"i));
SELECT (ART dist) IF (0 ("<e>"i) OR ("<i>"i) OR ("<të>"i) OR ("<së>"i))
					 (-1 ("<më>"i));
SELECT (ADV) IF (0 ("<më>"i))
				(1 ("<e>"i) OR ("<i>"i) OR ("<të>"i) OR ("<së>"i));
REMOVE (A sg indef) IF (-1 ("<e>"i) OR ("<i>"i))
					   (-2 ("<më>"i));
REMOVE (V) IF (-1 ("<e>"i) OR ("<i>"i) OR ("<të>"i))
			  (-2 ("<më>"i));
SELECT (abl) IF (-1 ("<së>"i))
				(-2 ("<më>"i));
SELECT (CLIT_PRO acc_1sg) IF (0 ("<më>"i))
							 (1 (V vt))
							 (NEGATE 1 ("<parë>"));
SELECT (CLIT_PRO dat_1sg) IF (0 ("<më>"i))
							 (1 (V vi) OR (V med))
							 (NEGATE 1 ("<parë>"));

# Alb më, të, etc.: clitic pronoun (resolve in analytical constructions, e.g. duke na shkruar)
SELECT (CLIT_PRO) IF (0 Clitics OR ("<e>"i))
					 (NEGATE 0 ("<u>"i))
				     (1 (ptcp))
					 (-1 ("<duke>"i));
SELECT (CLIT_PRO) IF (0 Clitics OR ("<e>"i))
					 (NEGATE 0 ("<u>"i))
				     (1 ("<ptcp>"i))
					 (-1 ("<të>"i) OR ("<t>"i))
					 (-2 ("<për>"i));
SELECT (CLIT_PRO) IF (0 Clitics OR ("<e>"i))
					 (NEGATE 0 ("<u>"i))
				     (1 ("<ptcp>"i))
					 (-1 ("<pa>"i))
					 (NEGATE -2 ("<e>"i));
SELECT (CLIT_PRO) IF (0 Clitics OR ("<e>"i))
					 (NEGATE 0 ("<të>"i) OR ("<u>"i))
				     (1 (ptcp Gheg))
					 (-1 ("<me>"i));
				 
# Alb të: Subjunctive particle between two verbs and after "do", "mund". The second verb is not participle.
SELECT (VB_PART) IF (0 ("të"i))
					(-1 (V))
					(1 (V))
					(NEGATE 1 (ptcp));
SELECT (VB_PART) IF (0 ("të"i))
					(-1 ("<do>"i) OR ("<mund>"i))
					(1 (V))
					(NEGATE 1 (ptcp) - (A));
					
# Alb mund: particle vs. verb in aorist vs. noun (resolve in forms with "të", e.g. mund të shkoj, mundi të shpëtonte).
SELECT (VB_PART) IF (0 ("<mund>"i))
				    (1 ("<të>"i) OR ("<t>"i) OR ("<ta>"i))
					(NEGATE 2 (A) OR (NOUN));
SELECT (V aor) IF (0 ("<mundi>"i))
				  (1 ("<të>"i) OR ("<t>"i) OR ("<ta>"i))
				  (2 (V));

# Alb do: future particle vs. verb
SELECT (VB_PART) IF (0 ("<do>"i))
					(1 (V));
SELECT (VB_PART) IF (0 ("<do>"i))
					(2 (V pl) OR (V 1 sg ipf) OR (V 1 sg pass) OR (V 1 sg sbjv));
SELECT (VB_PART) IF (0 ("<do>"i))
					(1 ("të"i))
					(2 Clitics)
					(3 (V pl) OR (V 1 sg ipf) OR (V 1 sg pass) OR (V 1 sg sbjv));
SELECT (VB_PART) IF (0 ("<do>"i))
					(1 ("të"i))
				    (2 ("na"i))
					(3 ("<i>"i) OR ("<e>"i))
					(4 (V pl) OR (V 1 sg ipf) OR (V 1 sg pass) OR (V 1 sg sbjv));
SELECT (VB_PART) IF (0 ("<do>"i))
				    (1 ("na"i))
					(2 ("<i>"i) OR ("<e>"i))
					(3 (V pl) OR (V 1 sg ipf) OR (V 1 sg pass) OR (V 1 sg sbjv));	

SECTION    # Other inter-POS homonymy
# Alb ajo: PRO vs. V (ajoj-imp.2sg)
REMOVE (V) IF (0 ("<ajo>"i));

# Preposition nga is not a form of the verb ngas.
# Не включать это правило в гегских текстах, т.к. там будет kam nga, jam nga
SELECT (PR) IF (0 ("<nga>"i))
			   (NEGATE -1 ("<u>"i));

# Alb dhe: and vs. give-aor.2sg vs. land
REMOVE (V) IF (0 ("<dhe>"i))
              (-1 ("<të>"i) OR ("<ju>"i) OR ("<nuk>"i) OR ("<mos>"i));
SELECT (V) IF (0 ("<dhe>"i))
              (-1 ("<na>"i) OR ("<i>"i) OR ("<u>"i) OR ("<ma>"i) OR ("<ia>"i));
SELECT (CONJ) IF (0 ("<dhe>"i))
				 (-1 ("<më>"i))
				 (-2 (V));
SELECT (CONJ) IF (0 ("<dhe>"i))
				 (-1 (NOUN))
				 (1 (NOUN));
SELECT (CONJ) IF (0 ("<dhe>"i))
				 (-1 (V))
				 (1 (V));
SELECT (CONJ) IF (0 ("<dhe>"i))
				 (-1 (A))
				 (1 (A));
SELECT (CONJ) IF (0 ("<dhe>"i))
				 (-1 (A))
				 (1 (ART))
				 (2 (A));
								 
# Alb nouns vs. adjectives vs. verbs in subjunctive: only verbs occur in constructions "do të jetë", "mund të gjeni", "(të) na e japin".
SELECT (V sbjv) IF (-1 ("të"i) OR ("<ta>"i))
				   (-2 ("<do>"i) OR ("<mund>"i));
SELECT (V sbjv) IF (-1 Clitics)
				   (-2 ("të"i))
				   (-3 ("<do>"i) OR ("<mund>"i));
SELECT (V sbjv) IF (-1 ("të"i))
				   (-2 ("na"i))
				   (-3 ("<i>"i) OR ("<e>"i));
SELECT (V sbjv) IF (-1 ("<do>"i) OR ("të"i))
				   (-2 ("na"i))
				   (-3 ("<i>"i) OR ("<e>"i));
SELECT (CLIT_PRO dat_1pl) IF (0 ("<na>"i))
							 (1 ("<i>"i) OR ("<e>"i));
SELECT (CLIT_PRO acc_3pl) IF (0 ("<i>"i))
							 (-1 ("<na>"i));
SELECT (CLIT_PRO acc_3sg) IF (0 ("<e>"i))
							 (-1 ("<na>"i));

# Alb me/në të vërtet(ë): "vërtet(ë)" is an accusative noun.
SELECT (NOUN sg acc indef) IF (0 ("<vërtetë>"i) OR ("<vërtet>"i))
							  (-1 ("<të>"i))
					          (-2 ("<me>"i) or ("<në>"i));
SELECT (ART f sg acc dist) IF (0 ("<të>"i))
							  (1 ("<vërtetë>"i) OR ("<vërtet>"i))
					          (-1 ("<me>"i) or ("<në>"i));
							  
# Alb verb "kam" vs. noun "ka" vs. preposition "ke" (resolve in analytical perfect forms, e.g. ka/ke/kanë filluar).
SELECT (V) IF (0 ("<kanë>"i) OR ("<ka>"i) OR ("<ke>"i))
				 (1 (ptcp));				 

# Alb duke: Gerund particle vs. noun "dukë" (resolve in analytical Gerund forms, e.g. duke filluar).
SELECT (VB_PART) IF (0 ("<duke>"i))
				    (1 (ptcp));	
SELECT (VB_PART) IF (0 ("<duke>"i))
				    (1 ("<na>"i))
					(2 ("<i>"i) OR ("<e>"i))
					(3 (ptcp));
SELECT (VB_PART) IF (0 ("<duke>"i))
					(1 Clitics OR ("<e>"i))
					(NEGATE 1 ("<të>"))
					(2 (ptcp));

# Disambiguation in constructions of the type "e kam të qartë", "sytë i kishte të fryrë".
SELECT (ART acc sg dist) IF (0 ("<të>"i))
				            (1 (A))
					        (-1 ("kam"i))
					        (-2 ("<e>"i));
SELECT (ART acc pl dist) IF (0 ("<të>"i))
				            (1 (A))
					        (-1 ("kam"i))
					        (-2 ("<i>"i));							
SELECT (CLIT_PRO) IF (0 ("<e>"i))
					 (1 ("kam"i))
					 (2 ("<të>"i))
					 (3 (A));
SELECT (CLIT_PRO acc_3pl) IF (0 ("<i>"i))
							 (1 ("kam"i))
							 (2 ("<të>"i))
							 (3 (A));
SELECT (A) IF (-1 ("<të>"i))
			  (-2 ("kam"i))
			  (-3 ("<e>"i) OR ("<i>"i));
REMOVE (A indef) IF (-1 ("<të>"i))
					(-2 ("kam"i))
					(-3 ("<e>"i) OR ("<i>"i));

# Disambiguation in constructions of the type "sa më parë".
SELECT (ADV) IF (0 ("<parë>"i) OR ("<mirë>"i))
				(-1 ("<më>"i))
				(-2 ("<sa>"i));
REMOVE (V) IF (0 ("<pak>"i))
			  (-1 ("<më>"i))
			  (-2 ("<sa>"i));
REMOVE (NOUN) IF (0 ("<shumë>"i))
				 (-1 ("<më>"i))
				 (-2 ("<sa>"i));